pluginManagement {
    repositories {
        gradlePluginPortal()
        // Ensure plugins can also be resolved from Maven Central (Gradle 9+ recommendation)
        mavenCentral()
    }
}

rootProject.name = 'module-proxy'

dependencyResolutionManagement {
    versionCatalogs {
        libs {
            // Import version catalog from published BOM
            from("ai.pipestream:pipeline-bom-catalog:0.1.2-SNAPSHOT")
        }
    }

    repositories {
        // Prefer Maven Central but allow locally published releases for development
        mavenLocal {
            mavenContent {
                releasesOnly()
            }
            content {
                includeGroupByRegex "ai\\.pipestream(\\..*)?"
                includeGroupByRegex "io\\.pipeline(\\..*)?"
            }
        }

        mavenCentral()

        maven {
            url = uri('https://oss.sonatype.org/content/repositories/snapshots/')
            mavenContent {
                snapshotsOnly()
            }
        }

        // GitHub Packages for ai.pipestream artifacts
        def githubToken = providers.environmentVariable("GITHUB_TOKEN")
        if (githubToken.isPresent()) {
            maven {
                url = uri("https://maven.pkg.github.com/ai-pipestream/pipeline-bom")
                credentials {
                    username = providers.environmentVariable("GITHUB_ACTOR").getOrElse("github-actions")
                    password = githubToken.get()
                }
                content {
                    includeGroupByRegex "ai\\.pipestream(\\..*)?"
                }
            }
        }

        if (System.getenv('CI_ENV') != 'internal') {
            maven {
                url = uri('https://repository.apache.org/content/repositories/snapshots/')
                allowInsecureProtocol = false
                content {
                    includeGroupByRegex "org\\.apache\\.tika(\\..*)?"
                }
            }
        }
    }
}
